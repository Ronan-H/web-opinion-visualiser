// lock definition
FUNCTION_BLOCK page_scoring

// define input variables
VAR_INPUT
    relevance : REAL;
    domain_usage : REAL;
    depth : REAL;
END_VAR

// define output variable
VAR_OUTPUT
    score : REAL;
END_VAR

// fuzzify input variable 'relevance'
FUZZIFY relevance
    TERM irrelevant := (0, 1) (0.03, 0);
    TERM minor := trape 0.01 0.06 0.2 0.26;
    TERM significant := (0.1, 0) (0.5, 1) (1, 1);
END_FUZZIFY

// fuzzify input variable 'domain_usage'
FUZZIFY domain_usage
    TERM low := sigm -30 0.15;
    TERM overused := sigm 30 0.2;
END_FUZZIFY

// fuzzify input variable 'depth'
FUZZIFY depth
    TERM shallow := (0, 1) (5, 1) (10, 0);
    TERM deep := (4, 0) (20, 1);
END_FUZZIFY

// defuzzify
DEFUZZIFY score
    TERM poor := (0, 0) (5, 1) (10, 0);
    TERM reasonable := (15, 0) (20, 1) (25, 0);
    TERM excellent := (30, 0) (35, 1) (40, 0);
    // Use 'Center Of Gravity' defuzzification method
    METHOD : COG;
    // Default value is 0 (if no rule activates defuzzifier)
    DEFAULT := 0;
END_DEFUZZIFY

RULEBLOCK No1
    // Use 'min' for 'and' (also implicit use 'max'
    // for 'or' to fulfill DeMorgan's Law)
    AND : MIN;
    // Use 'min' activation method
    ACT : MIN;
    // Use 'max' accumulation method
    ACCU : MAX;

    RULE 1 : IF relevance IS irrelevant OR domain_usage IS overused OR depth IS deep
                THEN score IS poor;

    RULE 2 : IF relevance IS minor AND domain_usage IS NOT overused
                THEN score IS reasonable;

    RULE 3 : IF relevance IS significant AND domain_usage IS low AND depth IS shallow
                THEN score IS excellent;


END_RULEBLOCK

END_FUNCTION_BLOCK
